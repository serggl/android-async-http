buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
        classpath 'com.github.dcendents:android-maven-plugin:1.0'
    }
}
apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
}

version '1.0.1'
group 'com.anjlab.android'

android {
    compileSdkVersion 3
    buildToolsVersion "19.0.0"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['examples','src']
            resources.srcDirs = ['examples','src']
            aidl.srcDirs = ['examples','src']
            renderscript.srcDirs = ['examples','src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepo) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Loopj Async HTTP'
            packaging 'aar'
            description 'Loopj Async HTTP lib with some improvements'
            url 'https://github.com/serggl/android-async-http.git'

            scm {
                url 'scm:git@github.com:serggl/android-async-http.git'
                connection 'scm:git@github.com:serggl/android-async-http.git'
                developerConnection 'scm:git@github.com:serggl/android-async-http.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'serggl'
                    name 'Sergey Glukhov'
                    email 'sergey.glukhov@gmail.com'
                }
            }
        }
    }
}
